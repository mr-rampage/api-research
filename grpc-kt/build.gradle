buildscript {
  ext.protobufVersion = '0.8.8'
  ext.protocVersion = '3.11.0'
  ext.vertxVersion = '3.9.0'
  ext.grpcVersion = '1.25.0'
  ext.junitJupiterEngineVersion = '5.4.0'

  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:$protobufVersion"
  }
}
plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.30'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
  id 'com.google.protobuf' version "$protobufVersion"
  id 'idea'
  id 'application'
}

group = 'grpc.kt'
version = '1.0.0-SNAPSHOT'

repositories {
  google()
  jcenter()
  mavenCentral()
  mavenLocal()
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}

def mainVerticleName = 'grpc.kt.app.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  implementation "io.vertx:vertx-grpc:$vertxVersion"
  implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"

  compileOnly "javax.annotation:javax.annotation-api:1.2"

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}

compileKotlin {
  kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
  kotlinOptions.jvmTarget = '1.8'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protocVersion"
  }
  plugins {
    grpc {
      artifact = "io.vertx:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc
    }
  }
}

shadowJar {
  classifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}
